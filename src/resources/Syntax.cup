import java_cup.runtime.Symbol;


parser code {:
    private Symbol s;
    
    private int _num_importes = 0;
    private int _num_clases = 0;
    private int _num_funciones = 0;

    public void syntax_error(Symbol s) {
        this.s = s;
    }

    public Symbol getS() {
        return this.s;
    }
:};


// ======== TERMINALES ========
// Palabras Reservadas 
terminal        ACCESMOD, NONACCESMOD, CLASS, PARENT, INTERFACE, ENUM, EXTENDS, IMPLEMENTS, CONTINUE,
                PASS, IMPORT, FROM, CREATE, DELETE, THIS, REFERENCE, ALIAS, ASYNCHRONOUS, PACKAGE, PRINT;
// Operadores
terminal        COMPARISON, SUMA, RESTA, PRODUCTO, DIVISION, MOD, POTENCIA, UNARY, ASSIGNMENT, ASSIGN_OPT,
                LOGICAL, NEGATION, NULLISH, TERNARY, IDENTITY, INSTANCE, TYPE, MEMBERSHIP, BITWISE;
// Visual Basic
terminal        HANDLER, MODIFIER, STORAGE, CAST, EVENT, GET, GOTO, MODULE, NAMESPACE, OF, OPERATOR, OPTION,
                PARTIAL, INCREMENT, STOP, STRUCTURE;
// SQL
terminal        ADD, CONSTRAINT, CONDITIONALQUERY, QUERY_CONDITIONS, ALTER, INSERT, SORT, BACKUP, RANGE,
                VIEW, COLUMN, TABLE, DATABASE, SELECT, JOIN, INDEX, LIMIT, UPDATE, UNION;
// Comentarios
//terminal        LINE_COMMENT, START_COMMENT, END_COMMENT, SPECIAL_COMMENT;
// Condicional
terminal        IF, ELIF, ELSE, SWITCH, CASE, BREAK,DEFAULT, TRY, TRY_CONSEQUENCE, EXCEPTION;
// Funciones
terminal        FUNCTION, VOID, LAMBDA, EXECUTE, RETURN;
// Separadores
terminal        PAR_IZQ, PAR_DER, LLAVE_IZQ, LLAVE_DER, COR_IZQ, COR_DER, POINT, COMMA, COLON, SEMI;
// Ciclos
terminal        FOR_LOOP, DO_LOOP, WHILE_LOOP, ITERATE;
// Tipos de dato
terminal        DATATYPE_BOOL, DATATYPE_INT, DATATYPE_DECIMAL, DATATYPE_COMPLEX,
                DATATYPE_STRING, DATATYPE_CHAR, DATATYPE_MISC, DATA_STRUCTURE, VARIABLE;
// Literales
terminal        BOOLEAN, NULL, STRING, CHARACTER, INTEGER, DECIMAL, COMPLEX;
// Otro
terminal        ID, ERROR, ARROW;


// ======== NO TERMINALES ========
non terminal    inicio, importar_opcional, importar, declara_importacion;
non terminal    clases_opcionales, clases, clase, atributos_opcionales, atributos;
non terminal    funciones_opcionales, funciones, funcion, tipo_funcion, parametros, parametros_opcionales,
                declara_parametros, mas_parametros, ejecutar_funcion;
non terminal    ciclo_while, evaluar_while, ciclo_do_while, ciclo_for, iterador, incremento;
non terminal    estructura_if, condicion_opcional, condicion_elif, condicion_else, estrucura_try;
non terminal    estructura_switch, switch_opciones, switch_literal, mas_switch_opciones, default_opcion,
                bloque_codigo_switch;

non terminal    ejecuta_paralelo, lista_funciones, otra_funcion_paralela;
non terminal    timeout, revisa, lista_id, mas_id, regresa_revision, haz_backup, variable_evento;
non terminal    valores_aleatorios, estructura_datos, valores_estructura, selecciona_aleatorio;
non terminal    ver_codigo_consola, conexion_bd, insertar_tabla;

non terminal    declara_variable, literal_numerico, tipo_dato, agregar_variable, asigna_variable,
                valor, variable_opr_unario;
non terminal    bloque_codigo, instrucciones, instrucciones_opcionales, declara_instruccion, imprime_consola, 
                exp_logica, exp_logica_p, comparacion, comparacion_p, operacion_logica, operacion_logica_p,
                valor_comp, exp_aritmetica, exp_a_p, termino, termino_p, factor;


// ======== PRECEDENCIA ========
precedence left SUMA, RESTA;
precedence left MOD;
precedence left PRODUCTO, DIVISION;
precedence right POTENCIA;

precedence left LOGICAL;
precedence left COMPARISON;


// ======== GRAMATICA ========
// Estructura del lenguaje (0)
start with inicio;
inicio ::= importar_opcional clases_opcionales funciones_opcionales instrucciones_opcionales
{:
    System.out.println("=============================");
    System.out.println("Librerias importadas: " + _num_importes);
    System.out.println("Clases declaradas: " + _num_clases);
    System.out.println("Funciones declaradas: " + _num_funciones);
    System.out.println("-----------------------------");
    System.out.println("Escaneo completado con exito.");
    System.out.println("=============================");
:};

// Importaciones (1)
importar_opcional ::= importar |
;
importar ::= declara_importacion SEMI importar_opcional
;
declara_importacion ::= IMPORT ID {: _num_importes++; :} |
                        FROM ID IMPORT ID {: _num_importes++; :}
;

// Clases (2)
clases_opcionales ::= clases |
;
clases ::= clase clases_opcionales
;
clase ::= CLASS ID LLAVE_IZQ atributos_opcionales funciones_opcionales LLAVE_DER {: _num_clases++; :}
;
atributos_opcionales ::= atributos |
;
atributos ::= declara_variable atributos_opcionales
;

// Funciones (2)
funciones_opcionales ::= funciones |
;
funciones ::= funcion funciones_opcionales
;
funcion ::= FUNCTION ID COLON tipo_funcion
;
tipo_funcion ::=    tipo_dato parametros LLAVE_IZQ instrucciones_opcionales RETURN valor SEMI LLAVE_DER
                    {: _num_funciones++; :}
                    | VOID parametros bloque_codigo {: _num_funciones++; :}
;
parametros ::= PAR_IZQ parametros_opcionales PAR_DER
;
parametros_opcionales ::= declara_parametros |
;
declara_parametros ::= tipo_dato ID mas_parametros
;
mas_parametros ::= COMMA declara_parametros |
;
ejecutar_funcion ::= ID PAR_IZQ PAR_DER
;

// Ciclo While (4)
ciclo_while ::= evaluar_while bloque_codigo
;
evaluar_while ::= WHILE_LOOP PAR_IZQ exp_logica PAR_DER
;

// Ciclo Do While (5)
ciclo_do_while ::= DO_LOOP bloque_codigo evaluar_while
;

// Ciclo For (6)
ciclo_for ::= FOR_LOOP PAR_IZQ iterador exp_logica SEMI incremento PAR_DER bloque_codigo
;
iterador ::= declara_variable | ID
;
incremento ::= agregar_variable | variable_opr_unario
;

// Switch (7)
estructura_switch ::= SWITCH PAR_IZQ ID PAR_DER LLAVE_IZQ switch_opciones default_opcion LLAVE_DER
;
switch_opciones ::= CASE COLON switch_literal bloque_codigo_switch mas_switch_opciones
;
switch_literal ::= CHARACTER | STRING | INTEGER | DECIMAL | COMPLEX
;
mas_switch_opciones ::= switch_opciones |
;
default_opcion ::= DEFAULT COLON bloque_codigo_switch
;
bloque_codigo_switch ::= LLAVE_IZQ instrucciones_opcionales BREAK SEMI LLAVE_DER
;

// if, elif, else (8)
estructura_if ::= IF PAR_IZQ exp_logica PAR_DER bloque_codigo condicion_opcional condicion_else
;
condicion_opcional ::= condicion_elif |
;
condicion_elif ::= ELIF PAR_IZQ exp_logica PAR_DER bloque_codigo condicion_opcional
;
condicion_else ::= ELSE bloque_codigo |
;

// Declara variables (9)
declara_variable ::=    DATATYPE_BOOL ID ASSIGNMENT exp_logica SEMI |
                        DATATYPE_INT ID ASSIGNMENT exp_aritmetica SEMI |
                        DATATYPE_DECIMAL ID ASSIGNMENT exp_aritmetica SEMI |
                        DATATYPE_COMPLEX ID ASSIGNMENT exp_aritmetica SEMI |
                        DATATYPE_STRING ID ASSIGNMENT STRING SEMI |
                        DATATYPE_CHAR ID ASSIGNMENT CHARACTER SEMI |
                        DATA_STRUCTURE ID ASSIGNMENT estructura_datos SEMI
;

// try, catch (10)
estrucura_try ::= TRY COLON bloque_codigo EXCEPTION COLON bloque_codigo
;


// ---------------- ESTRUCTURAS INVENTADAS ----------------
// Ejecucion de funciones en paralelo (11)
ejecuta_paralelo ::= PACKAGE EXECUTE COLON COR_IZQ lista_funciones COR_DER
;
lista_funciones ::= ejecutar_funcion otra_funcion_paralela
;
otra_funcion_paralela ::= COMMA lista_funciones |
;

// Timeout para la ejecucion de codigo (12)
timeout ::= STOP COR_IZQ INTEGER COR_DER bloque_codigo ELSE bloque_codigo
;

// Generar estructura con numeros aleatorios (13)
valores_aleatorios ::= POINT POINT POINT COR_DER PAR_IZQ INTEGER PAR_DER
;

// Revisar variables (14)
revisa ::= CONSTRAINT ID COLON COR_IZQ lista_id COR_DER SEMI
;
lista_id ::= ID mas_id
;
mas_id ::= COMMA lista_id |
;
regresa_revision ::= CONSTRAINT ID
;

// Backup de variables (15)
haz_backup ::= BACKUP COLON COR_IZQ lista_id COR_DER ARROW STRING SEMI
;

// (16)
selecciona_aleatorio ::= SELECT PAR_IZQ INTEGER PAR_DER OF COR_IZQ lista_id COR_DER
;

// Evento para escuchar cambios en una variable (17)
variable_evento ::= EVENT ID COLON ARROW bloque_codigo
;

// (18)
ver_codigo_consola ::= VIEW bloque_codigo
;

// (19)
conexion_bd ::= DATABASE ARROW STRING SEMI
;

// (20)
insertar_tabla ::= TABLE COR_IZQ STRING COR_DER ARROW COR_IZQ lista_id COR_DER SEMI
;


// ---------------- OTROS ----------------
// Variables
tipo_dato ::=   DATATYPE_BOOL |
                DATATYPE_INT |
                DATATYPE_DECIMAL |
                DATATYPE_COMPLEX |
                DATATYPE_STRING |
                DATATYPE_CHAR |
                DATATYPE_MISC |
                DATA_STRUCTURE
;
estructura_datos ::= COR_IZQ valores_estructura | selecciona_aleatorio
;
valores_estructura ::= valores_aleatorios | lista_id COR_DER
;
asigna_variable ::= ID ASSIGNMENT valor
;
valor ::=   exp_aritmetica | NULL | STRING | CHARACTER | BOOLEAN |
            EXECUTE ejecutar_funcion | regresa_revision
;
literal_numerico ::= INTEGER | DECIMAL | COMPLEX
;
agregar_variable ::= ID ASSIGN_OPT exp_aritmetica
;
variable_opr_unario ::= ID UNARY
;

// Estructras genericas
imprime_consola ::= PRINT PAR_IZQ STRING PAR_DER SEMI
;
bloque_codigo ::= LLAVE_IZQ instrucciones_opcionales LLAVE_DER
;
instrucciones_opcionales ::= instrucciones |
;
instrucciones ::= declara_instruccion instrucciones_opcionales
;
declara_instruccion ::= declara_variable |
                        asigna_variable SEMI|
                        agregar_variable SEMI |
                        variable_opr_unario SEMI |
                        ciclo_while |
                        ciclo_do_while |
                        ciclo_for |
                        estructura_if |
                        estructura_switch |
                        estrucura_try |
                        EXECUTE ejecutar_funcion SEMI|
                        imprime_consola |
                        ejecuta_paralelo |
                        timeout |
                        revisa |
                        haz_backup |
                        variable_evento |
                        ver_codigo_consola |
                        conexion_bd |
                        insertar_tabla
;

// Expresion logica
exp_logica ::=  ID exp_logica_p |
                BOOLEAN operacion_logica |
                valor_comp comparacion
;
exp_logica_p ::= comparacion | operacion_logica
;
comparacion ::= COMPARISON comparacion_p
;
comparacion_p ::= ID | valor_comp
;
operacion_logica ::= LOGICAL operacion_logica_p |
;
operacion_logica_p ::= ID | BOOLEAN
;
valor_comp ::= literal_numerico | STRING | CHARACTER | NULL
;

// Operaciones aritmeticas
exp_aritmetica ::= termino exp_a_p
;
exp_a_p ::= termino exp_a_p |
;
termino ::= factor termino_p
;
termino_p ::=   SUMA factor termino_p |
                RESTA factor termino_p |
                DIVISION factor termino_p |
                PRODUCTO factor termino_p |
                MOD factor termino_p |
                POTENCIA factor termino_p |
;
factor ::=  PAR_IZQ exp_aritmetica PAR_DER |
            literal_numerico |
            ID
;

// vim: set syntax=jflex:
